/**************************************************************************\
$VER: BBSLink List, v1.0.0 (Sep-19-2019) by k1ds3ns4t10n of -X-caliber BBS
\**************************************************************************/
options results;signal on SYNTAX;signal on ERROR;signal on IOERR
a=sourceline(2);parse var a . " " title ", " ver " (" vdate ") by " author " " .
tr=transmit;se=sendstring;gc=getchar;gu=getuser;gs=getscratch;mg=maygetchar
bbsidentify bbs;cv=left(word(result,3),4);cnet4=(datatype(cv,"n")=1&cv>"4.25")
bbsidentify name;g.bbsName=strip(result, 'B');esc="1b5b"x;cty="19"x;ctq="11"x
rs="rexxsupport.library";if ~show("l",rs) then if ~addlib(rs,0,-30) then exit
parse source . . fp .; df=left(fp,max(lastpos('/',fp),lastpos(':',fp)))
changewhere "BBSLink List"
/**************************************************************************/
/*                                                                        */
/*  C-Net BBSLink List                           -X-caliber BBS           */
/*    Used to display the list of BBS's             bbs.aholix.net:6800   */
/*    that are connected with BBSLink.net                                 */
/*                                                                        */
/*  Changelog:                                                            */
/*    v1.0.0 (19 Sep 2019) by k1ds3ns4t10n of -X-caliber BBS              */
/*                                                                        */
/*  Use at your own risk.                                                 */
/*                                                                        */
/*  INSTALATION INSTRUCTIONS:                                             */
/*                                                                        */
/*  1) Create a folder called 'BBSLink' in your PFiles: directory.        */
/*     Copy this file there.                                              */
/*                                                                        */
/*  2) Download http_get from Aminet and install in C:                    */
/*     https://aminet.net/package/comm/tcp/http_get                       */
/*                                                                        */
/*  3) Add a line to your SYSTEXT:sys.welcome file like this:             */
/*                                                                        */
/*     Q#0 pfiles:bbslink/bbslinklist}                                    */
/*                                                                        */
/*     ^ NOTE: "Q" here is a ctrl-Q                                       */
/*                                                                        */
/*  4) That's it!                                                         */
/*                                                                        */
/**************************************************************************/
/* SysOp Configurable Options *********************************************/

/* this path will be created as needed */
g.tempPath         = 'RAM:BBSLINK/' /* must end with a trailing slash */

/* these swtiches (0=off, 1=on) control how the List functions */
g.showStatus       = 1  /* Show http type status messages to the user */
g.autoRetry        = 1  /* Auto-retry web request if it times out or returns
                           a blank result. User may cancel the request by
                           pressing the ESC key */
g.debug            = 0  /* Show Errors to the SysOp's (account 1) screen.
                           This is for debug purposes and should not be left
                           on during normal operations */

/* ok to change these as needed for your system */
g.timeout          = 20 /* API http request timeout in seconds */

/* ok to change these messages, but do not add ANSI or MCI codes. */
s.retrieving       = 'Retrieving Data'
s.reqTimedOut      = 'Request timed out, please try again later.'
s.cancelled        = 'Request Cancelled'
s.escToCancel      = '(ESC to cancel)'
s.anyKey           = 'Smash any key to continue..'

/* End of SysOp Configurable Options **************************************/
/**************************************************************************/

c.err  = esc"0;31m"
c.blink= esc"5m"
c.reset= esc"0m"

if ~exists(g.tempPath) then address command'makedir 'strip(g.tempPath,"T","/")

gu 40; g.uid = space(result ,0)  /* account id */
gu 27; g.maxCol=result-1         /* term width */
g.debug = g.debug&g.uid==1

if ~exists("c:http_get") then signal badHttp

/* Show the list */
call getList
call processList
call displayList

/* reset ANSI env */
se ctq"Q1}"

EXIT:
  se s.anyKey
  gc
  tr
  exit

/* Procedures *************************************************************/

/* Delete the results file */
deleteFile: procedure expose tr g.
  parse arg filename

  filePath= g.tempPath||filename

  if exists(filePath) then do
    do delLoop = 1 to 3
      if delete(filepath) then leave delLoop
      call delay(25)
    end
    if g.debug & exists(filePath) then tr 'BBSLINKLIST: Unable to delete 'filePath' after 'delLoop' tries.'
  end
return

/* fetches web request
   @param uri       the url to retrieve
   @param fileName  the filename where to store results
   @param type      0=retrieve, 1=send

   returns results as an array called resultsArr structured as:
     resultArr.0 = line count
     resultArr.1 = line 1 string
     resultArr.2 = line 2 string
      .
      .
      .
     resultArr.n = line n string
*/
fetchWebRequest: procedure expose se tr mg esc ver s. g. resultArr.
  parse arg uri, fileName, type

  filePath=g.tempPath||fileName
  userAgent= "http_get/1.0 (AmigaOS 3.1; Amiga; 680x0; ks:40.68; wb:40.42) CNet/5 BBSLinkList/"strip(ver,'L','v')" ("g.bbsName")"

  /* run through outer loop at least once, repeat the loop if g.autoRetry is
     enabled and line count in resultArr.0 == 0 */

  do until ~g.autoRetry | resultArr.0>0
    /* clear any previous data in resultArr */
    drop resultArr.

    /* init resultArr */
    resultArr.0=0
    resultArr.1=""

    /* make web request */
    httpcmd = 'run >nil: http_get -u "'userAgent'" "'uri'" >'filePath
    address command httpcmd

    call time('r')

    /* show status message */
    if g.showStatus then do
      if type then reqType=s.sending
      else reqType=s.retrieving
      dotCount=0
      se reqType
    end

    /* inner loop checks if the results file is ready and exits if the
       file is ready or the timeout period has been reached */

    bufferflush
    do until time('e') > g.timeout + 1
      mg
      if result=="NOCHAR" then nop
      else if C2D(result)==27 then do
        /* ESC key will exit */
        resultArr.0 = 1
        resultArr.1 = s.cancelled
        leave /* cause loop to exit */
      end
      else call check

      call delay(25) /* 1/2 second */

      /* check if results file is ready yet */
      if exists(filePath) then do
        if open(BLW, filePath, R) then do
          /* read results file into resultsArr */
          line=0
          do while ~eof(BLW)
            line=line+1
            resultArr.line = readln(BLW)
          end
          /* place line count at resultArr.0 */
          resultArr.0 = line
          call close(BLW)
          leave /* cause loop to exit */
        end
        else if g.debug then tr "BBSLINKLIST: "filePath" exists but could not open"
      end
      else if g.debug then tr "BBSLINKLIST: "filePath" does not exist, could not open"

      /* update status message */
      if g.showStatus then do
        if dotCount==0 then se " "s.escToCancel
        else se "."
        dotCount = dotCount + 1
      end
    end
    bufferflush

    /* clear status message */
    if g.showStatus then do
      computeLen = length(reqType) + length(s.escToCancel) + dotCount + 1
      se esc||computeLen"D"copies(' ', computeLen)||esc||computeLen"D"
    end
  end

  /* check if request timed out */
  if resultArr.0==0 then do
    resultArr.0=1
    reslutArr.1=s.reqTimedOut
  end
return

/* get raw html list from BBSLink.net
   
   returns results as an array called resultsArr structured as:
     resultArr.0 = line count
     resultArr.1 = line 1 string
     resultArr.2 = line 2 string
      .
      .
      .
     resultArr.n = line n string
*/
getList: procedure expose se tr mg esc ver s. g. resultArr.

  filename= "listhtml"g.uid

  /* Delete the last results file in case it wasn't deleted last time */
  call deleteFile(filename)

  /* Fetch the list from BBSLink */
  /*uri="http://192.168.1.93/api.php?bbslinklist=1"*/
  uri="http://www.bbslink.net/list.php"
  call fetchWebRequest(uri, filename, 0)
  
   /* Delete the results file */
  call deleteFile(filename)
return


/* process raw html contained in resultArr. and pull out needed info.
   
   returns list data as an array called resultsArr structured as:
     resultsArr.0 = line count
     resultsArr.1 = line 1 string
     resultsArr.2 = line 2 string
      .
      .
      .
     resultsArr.n = line n string
*/
processList: procedure expose esc g. resultArr.
  LF = "0a"x; CR = "0d"x
  BBSLink = esc"1;33mBBS"esc"37mLink"esc"0;36m"

  /* init/clear arrays for processing data */
  drop data.
  data.0 = 0
  
  drop bbs.
  bbs.0 = 0
  
  /* pull out needed lines from resultArr., strip html tags, whitespace, LF and CR,
     then place the results into the data. array */
  startFound = 0
  startStr = '<p class="Header3">'
  endStr = '<span class="Header3">'
  count=1
  if resultArr.0 > 0 then do
    do i = 1 to resultArr.0
      if index(resultArr.i, startStr)~=0 then startFound = 1
      else if index(resultArr.i, endStr)~=0 then leave i
      if startFound then do
        tline = htmlStrip(resultArr.i)
        tline = strip(tline, "B", " "||LF||CR)
        data.count = tline      
        data.0 = count
        count = count + 1
      end
    end
  end
  else if g.debug then tr "BBSLINKLIST: resultArr is empty!"
  
  /* pull bbs info out of data. and place into an array called bbs. */
  /* TODO: combine this loop, with the loop above */
  maxName = 0
  maxLocation = 0
  count=1
  do i=16 to data.0 by 5
    if data.i~="" then do
      sysName = data.i
      nameLen = length(sysName)
      if nameLen > maxName then maxName = nameLen
      bbs.count.name = sysName
      
      j = i+1
      bbsLocation = data.j
      locationLen = length(bbsLocation)
      if locationLen > maxLocation then maxLocation = locationLen
      bbs.count.loc = bbsLocation
        
      j = i+2
      bbs.count.addr = data.j
      
      bbs.0 = count 
      count = count+1     
    end
  end
  
  /* clear resultArr. */
  drop resultArr.
  resultArr.0 = 0
  
  /* pull out BBSLink notice from data. and add to resultArr. */
  resultArr.1 = esc"0;36m"||replace(data.1, "BBSlink", BBSLink)
  resultArr.2 = ""
  resultArr.3 = esc"0;36m"||replace(data.2, "BBSlink", BBSLink)
  resultArr.4 = ""
  
  /* pull out column headers from data. */
  nameHeader = data.6
  locationHeader = data.9
  addressHeader = data.12 
  
  /* write system count to resultArr. */
  resultArr.5 = "There are currently "esc"1;37m"bbs.0||esc"0;36m systems using "BBSLink||esc"0;36m."
  resultArr.6 = ""

  /* write column headers to resultsArr. */
  count=7
  if maxName+maxLocation+1 > g.maxCol then nop
  else do
    headerLine = esc"1;33m"left(nameHeader, maxName+1, " ")||left(locationHeader, maxLocation+1, " ")
    if maxName+maxLocation+30 > g.maxCol then nop
    else headerLine = headerLine||addressHeader
    resultArr.count=headerLine
    count = count + 1
  end
  
  /* write list data to resultArr. */       
  do i=1 to bbs.0     
    if i // 2 == 0 then intensity=esc"1m"
    else intensity=esc"0m"
    if maxName+maxLocation+1 > g.maxCol then do
      resultArr.count = esc"0;35m"||center(bbs.i.name, g.maxCol)
      count = count + 1
      resultArr.count = esc"0;34m"||center(bbs.i.loc, g.maxCol)
      count = count + 1
      resultArr.count = esc"0;32m"||center(bbs.i.addr, g.maxCol)
      count = count + 1
      resultArr.count = ""
    end
    else do
      newLine = intensity||esc"35m"||left(bbs.i.name, maxName+1, " ")
      newLine = newLine||esc"34m"||left(bbs.i.loc, maxLocation+1, " ")
      if maxName+maxLocation+2+length(bbs.i.addr) > g.maxCol then do
        resultArr.count   = newLine
        count = count + 1
        resultArr.count = esc"32m"||right(bbs.i.addr, g.maxCol, " ")
      end
      else do
        resultArr.count = newLine||esc"32m"||bbs.i.addr
      end
    end
    count = count + 1
  end
  resultArr.0 = count
  resultArr.count = ""
return

/* show the list */  
displayList: procedure expose g. resultArr.
  
  /* open a file to write results to */
  filename = "listdisplay"||g.uid
  filePath = g.tempPath||filename
   
  if open(BLL, filePath, 'w') then do
    /* write BBSLink logo to results file */
    call writeln(BLL, "[1;33mÜÜÜ  ÜÜÜ   ÜÜÜ [37mÜ Ü")
    call writeln(BLL, "[1;33mÛÜÜß ÛÜÜß ßÜÜ  [37mÛ Ü ÜÜ  Û Ü")
    call writeln(BLL, "[1;33mÛÜÜß ÛÜÜß ÜÜÜß [37mÛ Û Û Û ÛßÜ")
    call writeln(BLL, "[1mÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ .net[0m")
    call writeln(BLL, "")
    
    /* write resultsArr. to results file */
    do i=1 to resultArr.0
      call writeln(BLL, resultArr.i)
    end
    
    call close(BLL)
  end
    
  /* display results file */
  cls
  sendfile filePath
  
  call deleteFile(filename)
return

/* quick and easy stripper of html tags */
htmlStrip: procedure
  parse arg html
 
  do while index(html, ">")~=0
    parse var html nonhtml "<" . ">" html
    html = nonhtml||html    
  end
return html

replace: procedure
  parse arg text, srch, repl
  if index(text, srch)==0 then return text

  slen = length(srch)
  tlen = length(text)
  do until tlen = 0
    tlen = lastpos(srch, text, tlen)
    if tlen ~= 0 then do
      text = insert(repl, delstr(text,tlen,slen), tlen - 1)
      tlen = tlen - 1
    end
  end
return text

/* word wraps an input string at a given width and will not split any words
   @param text  the input string to word wrap
   @param width the width to wrap at

   results will be placed into an array called resultsArr structured as:
     resultArr.0 = line count
     resultArr.1 = line 1 string
     resultArr.2 = line 2 string
      .
      .
      .
     resultArr.n = line n string
*/
wordWrap: procedure expose resultArr.
  parse arg text, width

  /* clear any previous data in the results array */
  drop resultArr.

  /* line count is held at position 0, init it to line 1 */
  resultArr.0 = 1

  if length(text) <= width then do
    resultArr.1 = text
    return
  end

  line = resultArr.0
  resultArr.1 = ""
  do while text ~= ""
    parse var text word text

    /* check if current line + next word < width */
    if length(resultArr.line) + length(word) + 1 <= width then do
      /* add word to current line */
      if resultArr.line=="" then resultArr.line = word
      else resultArr.line = resultArr.line" "word
    end
    else do
      /* start new line with with next word */
      line = line + 1
      resultArr.line = word
    end
  end

  /* store final line count at position 0 of the array */
  resultArr.0 = line
return

guruError: procedure expose tr se gc c. g.
  parse arg errMsg1, errMsg2
  cls
  tr c.err

  tr c.blink"Û"copies('ß', g.maxCol-2)"Û"

  call wordWrap(errMsg1, g.maxCol-4)
  do i=1 to resultArr.0
    tr c.blink"Û "c.reset||c.err|| center(resultArr.i, g.maxCol-4)||c.blink" Û"
  end

  call wordWrap(errMsg2, g.maxCol-4)
  do i=1 to resultArr.0
    tr c.blink"Û "c.reset||c.err|| center(resultArr.i, g.maxCol-4)||c.blink" Û"
  end

  tr c.blink"Û"copies('Ü', g.maxCol-2)"Û"c.reset
  tr
  se c.err|| center("Press ANY KEY to continue..", g.maxCol)
  gc
return

badHttp:
  errMsg1 = "Could not find http_get! Please re-read the install instructions."
  errMsg2 = "Guru Meditation #00000000:084DH77F"
  call guruError(errMsg1, errMsg2)
  exit

/**************************************************************************/

CHECK:
  if ARG() & ARG(1)~="###PANIC" then return ARG(1)
  getcarrier
  if result="TRUE" then do
    if ARG() then return ARG(1)
    else return
  end
  logentry "Lost Carrier!!"
  bufferflush
  exit

SYNTAX:
ERROR:
IOERR:
  e1="n1 Error: "rc" ("errortext(rc)")"
  e2="  Line: "left(sigl,4)"File:"
  c="`"fp", "ver"'"
  e2=e2" "c
  tr e1
  tr e2
  logentry e1
  logentry e2
  e=strip(translate(sourceline(sigl),"\{",""))
  do while e~=""
    e3="Source: "left(e,37)
    tr e3
    logentry e3
    e=substr(e,38)
    end
  bufferflush
  exit
/**************************************************************************\
\*************************** -X-caliber BBS telnet://bbs.aholix.net:6800 **/